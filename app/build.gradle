apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    buildToolsVersion = versions.buildTools
    compileSdkVersion versions.compileSdk

    defaultConfig {
        applicationId "com.akhbulatov.discusim"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode 1
        versionName "1.0"
        resConfigs "en", "ru"

        buildConfigField "String", "BASE_URL", "\"https://disqus.com/\""
        buildConfigField "String", "BASE_API_URL", "\"https://disqus.com/api/3.0/\""

        buildConfigField "String", "CLIENT_ID", keystoreProperties['disqus_client_id']
        buildConfigField "String", "CLIENT_SECRET", keystoreProperties['disqus_client_secret']
        buildConfigField "String", "REDIRECT_URI", keystoreProperties['disqus_redirect_uri']
        buildConfigField "String", "SCOPE", keystoreProperties['disqus_scope']
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties['release_store_file'])
            storePassword keystoreProperties['release_store_password']
            keyAlias keystoreProperties['release_key_alias']
            keyPassword keystoreProperties['release_key_password']
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            debuggable true
        }

        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    androidExtensions {
        experimental = true
    }
}

configurations {
    ktlint
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}"

    // Android UI
    implementation "com.google.android.material:material:${versions.material}"
    implementation "androidx.appcompat:appcompat:${versions.appCompat}"
    implementation "androidx.fragment:fragment-ktx:${versions.fragment}"
    implementation "androidx.recyclerview:recyclerview:${versions.recyclerView}"
    implementation "androidx.constraintlayout:constraintlayout:${versions.constraintLayout}"

    // Firebase
    implementation "com.google.firebase:firebase-core:${versions.firebaseCore}"

    // Custom Views
    implementation "com.github.razir.progressbutton:progressbutton:${versions.progressButton}"
    implementation "org.sufficientlysecure:html-textview:${versions.htmlTextView}"

    // DI
    implementation "com.google.dagger:dagger:${versions.dagger}"
    implementation "com.google.dagger:dagger-android-support:${versions.dagger}"
    kapt "com.google.dagger:dagger-compiler:${versions.dagger}"
    kapt "com.google.dagger:dagger-android-processor:${versions.dagger}"

    // Rx
    implementation "io.reactivex.rxjava2:rxjava:${versions.rxJava}"
    implementation "io.reactivex.rxjava2:rxkotlin:${versions.rxKotlin}"
    implementation "io.reactivex.rxjava2:rxandroid:${versions.rxAndroid}"

    // Network
    implementation "com.squareup.retrofit2:retrofit:${versions.retrofit}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${versions.retrofit}"
    implementation "com.squareup.retrofit2:converter-moshi:${versions.retrofit}"
    implementation "com.squareup.okhttp3:logging-interceptor:${versions.okhttp}"

    // JSON
    kapt "com.squareup.moshi:moshi-kotlin-codegen:${versions.moshi}"

    // Navigation
    implementation "ru.terrakok.cicerone:cicerone:${versions.cicerone}"

    // Image Loading
    implementation "com.github.bumptech.glide:glide:${versions.glide}"

    // Date & Time
    implementation "com.jakewharton.threetenabp:threetenabp:${versions.threeTenAbp}"

    // Extensions
    implementation "androidx.core:core-ktx:${versions.androidKtx}"
    implementation "org.jetbrains.anko:anko-commons:${versions.anko}"
    implementation "org.jetbrains.anko:anko-support-v4-commons:${versions.anko}"

    // Dev Tools
    ktlint "com.pinterest:ktlint:${versions.ktlint}"
    implementation "com.jakewharton.timber:timber:${versions.timber}"
    implementation "com.facebook.stetho:stetho:${versions.stetho}"
    implementation "com.facebook.stetho:stetho-okhttp3:${versions.stetho}"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:${versions.leakCanary}"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "src/**/*.kt"
}
